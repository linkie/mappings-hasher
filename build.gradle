plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

group 'me.shedaniel.quiltmc'

def runNumber = System.getenv("GITHUB_RUN_NUMBER") ?: "9999"
version "1.0.$runNumber"

// Target JDK 8
tasks.withType(JavaCompile).configureEach {
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    } else {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

application {
    mainClass = 'me.shedaniel.quiltmc.mappings_hasher.Main'
}

repositories {
    mavenCentral()
    maven {
        name = "Quilt"
        url = "https://maven.quiltmc.org/repository/release"
    }
}

dependencies {
    shadow 'org.quiltmc:quilt-json5:1.0.0'
    shadow 'org.quiltmc:lorenz-tiny:3.0.0'
    shadow 'org.cadixdev:lorenz-io-proguard:0.5.6'
    shadow "org.ow2.asm:asm:9.1"
}

tasks.getByName("run").setArgsString(minecraftVersion)

jar {
    archiveClassifier = "raw"
}

shadowJar {
    archiveClassifier = null
    configurations = [project.configurations.shadow]
    relocate "org.quiltmc", "me.shedaniel.mappings_hasher.quiltmc"
    relocate "net.fabricmc", "me.shedaniel.mappings_hasher.fabricmc"
    relocate "org.cadixdev", "me.shedaniel.mappings_hasher.cadixdev"
    relocate "org.objectweb.asm", "me.shedaniel.mappings_hasher.asm"
    relocate "me.jamiemansfield", "me.shedaniel.mappings_hasher.jamiemansfield"
}

[configurations.apiElements, configurations.runtimeElements].each { config ->
    (components.java as AdhocComponentWithVariants).withVariantsFromConfiguration(config) {
        configurationVariant.artifacts.removeIf { artifact ->
            artifact.file == tasks.jar.archiveFile.get().asFile
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact tasks.shadowJar
        }
    }
    repositories {
        def ENV = System.getenv()

        if (ENV.MAVEN_PASS) {
            maven {
                url = "https://deploy.shedaniel.me/"
                credentials {
                    username = "shedaniel"
                    password = ENV.MAVEN_PASS
                }
            }
        }
    }
}